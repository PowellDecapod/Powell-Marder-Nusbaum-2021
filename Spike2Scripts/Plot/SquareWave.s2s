'"SquareWave". Script to create a waveform channel with square signals to use as a TTL trigger. Trigger times are taken from an event channel.

'Dirk Bucher, 03/2013

if ViewKind() <> 0 then		'checks if the associated window is a time view...
   Message("This isn't a time view!");
  	halt							'...if not, script is stopped
endif

WindowVisible (3);		'sets the time view to the front

var ok%, cur1, cur2, wholefile%;	'sets variable expressions	

if Cursor (1) <> 0 and Cursor (2)/Cursor (1)<1 or Cursor (1)=-1 or Cursor (2)=-1 then ' if Cursor (1) and Cursor (2) are not present or not in the right
																						'order....
	DlgCreate ("ATTENTION!");			
	DlgText ("Cursor (1) and (2) not set correctly.",2,1);
	DlgLabel	(1, "Process whole file?",2,2);								'...choose if you want to analyse the whole file
	ok%:=DlgShow (wholefile%);

	if ok%=1 then																	'sets analysis to whole file or...
		cur1:= 0;														
		cur2:=MaxTime ();
	else
  		halt																		'stops the script
	endif

else
	cur1 := Cursor(1);															'if present and in the right order, get cursor times	
	cur2 := Cursor(2);
endif

var ok1%,evntCh%,outch%,ChName$,Int,Amp,Dur;

DlgCreate("Assign");
DlgChan(1,"Event Channel",2);
DlgChan(2, "Output Channel #",128);
DlgString(3,"New Channel Name",15);
DlgReal(4,"Data Interval [ms]",0.01,100);
DlgReal(5,"Step Amplitude [V]",-5,5);
DlgReal(6,"Step Duration [ms]",0.1,100);
ChName$:="Pulses";
Int:=0.1;
Amp:=5;
Dur:=0.5;
ok1%:=Dlgshow(evntCh%,outch%,ChName$,Int,Amp,Dur);
if ok1%<>1 then
    halt
endif
Int:=Int/1000;
Dur:=Dur/1000;

var pulse[10000], nPts%;
nPts%:=Dur/Int;
ArrAdd(pulse[0:nPts%],Amp);

var virtCh%, mCh%;

virtCh%:=VirtualChan(0, "");
VirtualChan(virtCh%, "WSin(0.1, 0)*0", 0, Int, 0);
mCh%:=MemChan(1,0,Int);
MemImport(mCh%,virtCh%,0,MaxTime());


var evts[100000], nEvt%, k%;
nEvt%:=ChanData(evntCh%,evts[],cur1,cur2);

for k%:=0 to nEvt%-1 step 1 do
    ChanWriteWave(mCh%,pulse[0:nPts%],evts[k%]);
next
ChanSave(mCh%,outch%);
ChanDelete(mch%,0);
ChanDelete(virtCh%,0);
ChanShow(outch%);
Optimise(outch%);
ChanTitle$(outch%,ChName$);


FrontView (App (3));		'brings script to front...
FileClose (0);				'...and closes it
    


